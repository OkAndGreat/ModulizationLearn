plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

println("log from app build.gradle file")

android {
    compileSdk commonConfig.compileSdk

    defaultConfig {
        applicationId appId.app
        minSdk commonConfig.minSdk
        targetSdk commonConfig.targetSdk
        versionCode commonConfig.versionCode
        versionName commonConfig.versionName

        testInstrumentationRunner commonConfig.testInstrumentationRunner

        // 让Java代码也可以得到gradle里的信息
        // 组件化 和 集成化 的时候需要
        buildConfigField("boolean", "isRelease", String.valueOf(isRelease))
    }

    buildTypes {
        debug {
            buildConfigField("String", "debug", "\"${url.debug}\"")
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    dependenciesConfig.each { k, v -> implementation v }

    if (isRelease) {
        //这里的依赖如果模块很多也可以使用统一管理
        implementation project(':login')
        implementation project(':personal')
    }
}